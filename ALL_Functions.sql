CREATE FUNCTION [dbo].[FN_RANDOM]()
RETURNS FLOAT
AS
BEGIN
	RETURN TRY_CAST((SELECT [RANDOM] FROM [VW_RANDOM]) AS FLOAT);
END
GO

CREATE FUNCTION [dbo].[GetDateInRange] (@START DATETIME, @END DATETIME)
RETURNS DATE
AS
BEGIN
	DECLARE @DATE DATE = TRY_CONVERT(DATE, @START);

	IF (@END IS NULL)
	BEGIN
		SET @END = TRY_CONVERT(DATE, CURRENT_TIMESTAMP);
	END

	IF(@START > @END)
	BEGIN
		SET @START = TRY_CONVERT(DATE, @END);
		SET @END = @DATE;
	END
	
	SET @END = TRY_CONVERT(DATE, @END);

	IF (@DATE != @END)
	BEGIN
		DECLARE @D_DIFFERENCE INT = TRY_CAST(dbo.FN_RANDOM()*DATEDIFF(DAY, @START, @END) AS INT);
		SET @DATE = DATEADD(DAY, @D_DIFFERENCE, @START);
	END
	RETURN @DATE;
END
GO

CREATE FUNCTION [dbo].[GetDateTimeInRange] (@START DATETIME, @END DATETIME)
RETURNS DATETIME
AS
BEGIN
	DECLARE @DATE DATETIME = @START;

	IF @END IS NULL
	BEGIN
		SET @END = CURRENT_TIMESTAMP;
	END

	IF(@START > @END)
	BEGIN
		SET @START = @END;
		SET @END = @DATE;
	END
	
	IF (@START != @END)
	BEGIN

		DECLARE @D_DIFFERENCE INT = DATEDIFF(DAY, @START, @END);
		DECLARE @S_DIFFERENCE INT = TRY_CAST(dbo.FN_RANDOM() * 86400 AS INT);
	
		IF(@D_DIFFERENCE < 1)
		BEGIN
			SET @S_DIFFERENCE %= DATEDIFF(SECOND, @START, @END);
		END
		ELSE
		BEGIN
			SET @D_DIFFERENCE = TRY_CAST(dbo.FN_RANDOM() * @D_DIFFERENCE AS INT);
		END

		SET @DATE = DATEADD(DAY, @D_DIFFERENCE, @START);
		SET @DATE = DATEADD(SECOND, @S_DIFFERENCE, @DATE);
	END
	RETURN @DATE;
END
GO
